( main.tal )
( devices )
|10 @Console &vector $2 &read $1 &pad $5 &write $1 &error $1

%EMIT { .Console/write DEO }
%NewLine { #0a .Console/write DEO }
%PrintSpace { #20 .Console/write DEO }

%ToMask { #ff EOR #01 ADD }
%PrintNibble ( nibble -- ) {
    DUP LIT "0 ADD SWP
    #09 GTH ToMask
    LIT2 "a "9 #01 ADD SUB
    AND ADD .Console/write DEO
}
%PrintByte ( byte -- ) {
    DUP
    #04 SFT PrintNibble
    #0f AND PrintNibble
}
%PrintAddr ( short -- ) { LDA PrintByte }
%PrintShort ( short -- ) { SWP PrintByte PrintByte }

( init )
|0100
    ;initStack JSR2

    ;Stack ;Stack/data #000f ADD2
    ;DumpMem JSR2
    NewLine
    
    ;Stack/pointer PrintShort
    NewLine
    ;Stack/pointer LDA2 PrintShort
    NewLine

    #2806 push DUP2
    PrintShort NewLine
    LDA2 PrintShort
    NewLine NewLine

    ;Stack ;Stack/data #000f ADD2
    ;DumpMem JSR2
    NewLine

BRK

( print.tal )

@DumpLine ( short -- )
    #04 SFT2 #40 SFT2   ( short_addr -- )
    DUP2 PrintShort
    LIT ": EMIT

    #0000                   ( short_addr 0000 -- )
    &forloop                  ( short_addr short_i -- )
        DUP2 #000f SWP2     ( short_addr short_i 10 short_i -- )
        LTH2 ;&endfor JCN2    ( short_addr short_i -- )
        PrintSpace
        SWP2 DUP2             ( short_i short_addr short_addr -- )
        PrintAddr             ( short_i short_addr -- )
        INC2 SWP2 INC2        ( short_addr+1 short_i+1 -- )
        ;&forloop JMP2
    &endfor
        POP2 POP2             ( -- )
JMP2r

@DumpMem ( short_addr1 short_addr2 -- )
    OVR2 OVR2 SWP2      ( short_addr1 short_addr2 short_addr2 short_addr1 -- )
    #0000 ROT2 ROT2   ( short_addr1 short_addr2 0000 short_addr2 short_addr1 -- )
    LTH2 ;&end JCN2     ( short_addr1 short_addr2 0000 -- )
    POP2                ( short_addr1 short_addr2 -- )
    OVR2 SUB2 #04 SFT2  ( short_addr1 short_delta -- )

    DUP2 PrintShort
    PrintSpace PrintSpace PrintSpace
    LIT2 "f "e LIT2 "d "c LIT2 "b "a LIT2 "9 "8
    LIT2 "7 "6 LIT2 "5 "4 LIT2 "3 "2 LIT2 "1 "0
    EMIT PrintSpace PrintSpace EMIT PrintSpace PrintSpace
    EMIT PrintSpace PrintSpace EMIT PrintSpace PrintSpace
    EMIT PrintSpace PrintSpace EMIT PrintSpace PrintSpace
    EMIT PrintSpace PrintSpace EMIT PrintSpace PrintSpace
    EMIT PrintSpace PrintSpace EMIT PrintSpace PrintSpace
    EMIT PrintSpace PrintSpace EMIT PrintSpace PrintSpace
    EMIT PrintSpace PrintSpace EMIT PrintSpace PrintSpace
    EMIT PrintSpace PrintSpace EMIT NewLine
    
    #0000  ( short_addr short_delta 0000 -- )
    &forloop                        ( short_addr short_delta short_i -- )
        LTH2k ;&end JCN2            ( short_addr short_delta short_i -- )
        ROT2 DUP2 ;DumpLine JSR2    ( short_delta short_i short_addr -- )
        NewLine
        #0010 ADD2                ( short_delta short_i short_addr+16 -- )
        ROT2 ROT2 INC2              ( short_addr+16 short_delta short_i+1 -- )
        ;&forloop JMP2
    &end
        POP2 POP2 POP2
JMP2r


@initStack ( -- )
    ;Stack/data ;Stack/pointer STA2
JMP2r

@push ( obj -- addr )
    ;Stack/pointer LDA2     ( obj ptr*              )
    DUP2 INC2 INC2          ( obj ptr* ptr*+2       )
    ;Stack/data LTH2        ( obj ptr* ptr*+2<&data )
    ,&fail JCN              ( obj ptr*              )
    ( -- Checker ici -- )
        DUP2 ROT2 ROT2      ( ptr* obj ptr*         )
        STA2                ( ptr*       ptr* = obj )
        DUP2 INC2 INC2      ( ptr* ptr*+2           )
        ;Stack/pointer STA2 ( ptr*     ptr = ptr*+2 )
        ,&end JMP
    &fail
        POP2 POP2
        #0000
    &end
JMP2r

@Stack
    &pointer $2
    &data

